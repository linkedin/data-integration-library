assert plugins.hasPlugin(JavaPlugin)

if (project.hasProperty('disableShipkit')) {
  return
}

apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

////////////////////////////////////////////////
// Artifact setup
////////////////////////////////////////////////

tasks.withType(Jar) {
  from "$rootDir/LICENSE"
  from "$rootDir/NOTICE"
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

javadoc {
  options.addStringOption('Xdoclint:none', '-quiet')
  options.addStringOption('Xwerror', '-quiet')
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar
  archives javadocJar

  if (plugins.hasPlugin('pegasus')) {
    archives mainDataTemplateJar
  }
}

def pomConfig = {
  licenses {
    license {
      name = 'BSD 2-CLAUSE LICENSE'
      url = 'https://github.com/linkedin/data-integration-library/blob/master/LICENSE'
      distribution = 'repo'
    }
  }
  scm {
    url = 'https://github.com/linkedin/data-integration-library.git'
  }
  issueManagement {
    url = 'https://github.com/linkedin/data-integration-library/issues'
    system = 'GitHub issues'
  }
}

publishing {
  publications {
    if (!project.ext.has('publications') || project.ext.publications.contains('jar')) {
      jar(MavenPublication) {
        from components.java

        artifact javadocJar
        artifact sourcesJar

        pom pomConfig
      }
    }

    if (plugins.hasPlugin('pegasus') &&
        (!project.ext.has('publications') || project.ext.publications.contains('dataTemplate'))) {
      dataTemplate(MavenPublication) {
        artifact mainDataTemplateJar
        artifactId "$project.name-data-template"

        pom pomConfig
      }
    }
  }
}

////////////////////////////////////////////////
// Artifactory publishing
////////////////////////////////////////////////

artifactory {
  contextUrl = 'https://linkedin.jfrog.io/artifactory'
  publish {
    repository {
      repoKey = 'dil'
      username = System.getenv('ARTIFACTORY_USER')
      password = System.getenv('ARTIFACTORY_KEY')
    }

    defaults {
      if (project.ext.has('publications')) {
        publications(*project.ext.publications)
      } else {
        publications('jar')

        if (plugins.hasPlugin('pegasus')) {
          publications('jar', 'dataTemplate')
        }
      }
    }
  }
}

artifactoryPublish {
  skip = project.hasProperty('artifactory.dryRun')
}
